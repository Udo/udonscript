/*
You're sure that's the right password, but the door won't open. You knock, but nobody answers. You build a snowman while you think.

As you're rolling the snowballs for your snowman, you find another security document that must have fallen into the snow:

"Due to newer security protocols, please use password method 0x434C49434B until further notice."

You remember from the training seminar that "method 0x434C49434B" means you're actually supposed to count the number of times any click causes the dial to point at 0, regardless of whether it happens during a rotation or at the end of one.

Following the same rotations as in the above example, the dial points at zero a few extra times during its rotations:

    The dial starts by pointing at 50.
    The dial is rotated L68 to point at 82; during this rotation, it points at 0 once.
    The dial is rotated L30 to point at 52.
    The dial is rotated R48 to point at 0.
    The dial is rotated L5 to point at 95.
    The dial is rotated R60 to point at 55; during this rotation, it points at 0 once.
    The dial is rotated L55 to point at 0.
    The dial is rotated L1 to point at 99.
    The dial is rotated L99 to point at 0.
    The dial is rotated R14 to point at 14.
    The dial is rotated L82 to point at 32; during this rotation, it points at 0 once.

In this example, the dial points at 0 three times at the end of a rotation, plus three more times during a rotation. So, in this example, the new password would be 6.

Be careful: if the dial were pointing at 50, a single rotation like R1000 would cause the dial to point at 0 ten times before returning back to 50!

Using password method 0x434C49434B, what is the password to open the door?
*/

var test_instructions = 'L68
L30
R48
L5
R60
L55
L1
L99
R14
L82'.split("\n")

var input_path = "scripts/aoc2025/day01/input.txt"
var dial_size = 100
var start_position = 50

// Count how many times a rotation crosses or lands on 0.
function zero_hits_in_move(pos, direction, distance) {
    var hits = 0
    if (distance <= 0)
        return hits

    if (pos == 0) {
        hits = floor(distance / dial_size)
    } else if (direction == "L") {
        var steps_to_zero = pos
        if (distance >= steps_to_zero)
            hits = 1 + floor((distance - steps_to_zero) / dial_size)
    } else { // "R"
        var steps_to_zero = dial_size - pos
        if (distance >= steps_to_zero)
            hits = 1 + floor((distance - steps_to_zero) / dial_size)
    }
    return hits
}

function count_zero_hits(lines) {
    var pos = start_position
    var zeros = 0
    var n = lines.len()
    for (var i = 0; i < n; i = i + 1) {
        var line = trim(lines[i])
        if (line.len() == 0)
            continue
        var direction = line.substr(0, 1)
        var distance = to_int(line.substr(1))
        zeros = zeros + zero_hits_in_move(pos, direction, distance)
        if (direction == "L")
            pos = mod_floor(pos - distance, dial_size)
        else if (direction == "R")
            pos = mod_floor(pos + distance, dial_size)
    }
    return zeros
}

function solve(text) {
    return count_zero_hits(text.split("\n"))
}

function main() {
    var example = count_zero_hits(test_instructions)
    print("Example check (expected 6):", example)

    var input_text = read_entire_file(input_path)
    var answer = solve(input_text)
    print("Part 2 password:", answer)
}
